import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.equationl.giteetodo"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String", "CLIENT_ID", properties.getProperty("CLIENT_ID")
        buildConfigField "String", "CLIENT_SECRET", properties.getProperty("CLIENT_SECRET")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
//    composeOptions {
//        kotlinCompilerExtensionVersion "1.5.14"
//    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    buildFeatures {
        buildConfig = true
    }
    namespace 'com.equationl.giteetodo'
}

// 用于忽略使用 OptIn 注解的警告
tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation platform('androidx.compose:compose-bom:2024.06.00')
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.animation:animation:1.7.0-beta03"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.2'
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.8.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.2'
    implementation "androidx.navigation:navigation-compose:2.7.7"
    implementation "androidx.datastore:datastore-preferences:1.1.1"
    implementation "androidx.datastore:datastore-core:1.1.1"
    implementation "androidx.paging:paging-runtime-ktx:3.3.0"
    implementation "androidx.paging:paging-compose:3.3.0"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.glance:glance-appwidget:1.1.0"
    implementation "androidx.room:room-paging:2.6.1"
    implementation "com.google.dagger:hilt-android:2.51.1"
    kapt "com.google.dagger:hilt-compiler:2.51.1"
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    implementation "io.coil-kt:coil-compose:2.6.0"

    implementation "com.halilibo.compose-richtext:richtext-commonmark:0.20.0"
    implementation "com.halilibo.compose-richtext:richtext-ui-material3:0.20.0"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation "com.airbnb.android:lottie-compose:6.3.0"
    implementation "io.github.vanpra.compose-material-dialogs:color:0.9.0"

    // see: https://developer.android.com/studio/write/java8-support#library-desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.6.8"
    debugImplementation "androidx.compose.ui:ui-tooling"
}

kapt {
    correctErrorTypes true
}